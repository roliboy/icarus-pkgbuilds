# Maintainer: Nagy Roland <roliboy@protonmail.com>
# Contributor: dkasak <dkasak AT termina DOT org DOT uk>
# Contributor: nblock <nblock [/at\] archlinux DOT us>
# Contributor: DigitalPathogen <aur@digitalpathogen.co.uk>
# Contributor: daronin
# Contributor: kageurufu

pkgname=burpsuite
pkgver=2020.12.1
pkgrel=1
pkgdesc="An integrated platform for performing security testing of web applications"
url="https://portswigger.net/burp/"
depends=('java-runtime>=9')
makedepends=('unzip' 'zip')
arch=('any')
license=('custom')
noextract=("${pkgname}-${pkgver}.jar")
source=("${pkgname}-${pkgver}.jar::https://portswigger.net/burp/releases/download?product=community&version=${pkgver}&type=Jar"
        LICENSE
        burpsuite.desktop
        icon64.png
        IcarusTheme.properties)
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP')

build() {
  unzip "${pkgname}-${pkgver}.jar" -d unpacked
  cd unpacked
  mv ../IcarusTheme.properties burp/theme/BurpDarkLaf.properties
  rm chromium*
  zip -r "${pkgname}-${pkgver}-icarus.jar" *
  mv "${pkgname}-${pkgver}-icarus.jar" "../${pkgname}-${pkgver}.jar"
}

package() {
  mkdir -p ${pkgdir}/usr/bin
  mkdir -p ${pkgdir}/usr/share/{applications,pixmaps,${pkgname},licenses/${pkgname}}

  install -m644 ${pkgname}-${pkgver}.jar ${pkgdir}/usr/share/${pkgname}/${pkgname}.jar
  install -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/
  install -m644 burpsuite.desktop ${pkgdir}/usr/share/applications/
  install -m644 icon64.png ${pkgdir}/usr/share/pixmaps/burpsuite.png

  # Create startup file for burpsuite.
  echo "#!/bin/sh" > ${pkgdir}/usr/bin/${pkgname}
  echo "exec \$JAVA_HOME/bin/java -Dawt.useSystemAAFontSettings=on -Dswing.aatext=true -jar /usr/share/burpsuite/burpsuite.jar \$@" >> ${pkgdir}/usr/bin/${pkgname}
  chmod 755 ${pkgdir}/usr/bin/${pkgname}
}
