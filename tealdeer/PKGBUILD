# Maintainer: Danilo Bargen <aur at dbrgn dot ch>
# PGP key is on keyservers and on keybase (dbrgn)
pkgname=tealdeer
_pkgname=tealdeer
_binname=tldr
pkgver=1.4.1
pkgrel=1
pkgdesc="A fast tldr client in Rust. Static binaries from GitHub."
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
url="https://github.com/dbrgn/tealdeer/"
license=('MIT' 'Apache')
provides=('tldr')
conflicts=('tldr')
validpgpkeys=('EA456E8BAF0109429583EED83578F667F2F3A5FA')
source=(
  "completions_bash_v${pkgver}::https://github.com/dbrgn/${_pkgname}/releases/download/v${pkgver}/completions_bash"
  "completions_fish_v${pkgver}::https://github.com/dbrgn/${_pkgname}/releases/download/v${pkgver}/completions_fish"
  "completions_zsh_v${pkgver}::https://github.com/dbrgn/${_pkgname}/releases/download/v${pkgver}/completions_zsh"
  "LICENSE-MIT-v${pkgver}::https://github.com/dbrgn/${_pkgname}/releases/download/v${pkgver}/LICENSE-MIT"
  "${pkgname}-bin-x86_64-v${pkgver}::https://github.com/dbrgn/${_pkgname}/releases/download/v${pkgver}/tldr-linux-x86_64-musl"
  "${pkgname}-bin-x86_64-v${pkgver}.sig::https://github.com/dbrgn/${_pkgname}/releases/download/v${pkgver}/tldr-linux-x86_64-musl.sig"
)

sha256sums=(
  'd867f58919431a1097237c5a8118cf4dcb01aadf960f61eddd432bf63050971d'
  '5fdf3346a03157f9a43f42e5d475611cce6b3685badf1084db2587f2ce425931'
  '56cad3fdd615c040ded73de1e75342156e903a501806b487b526dce1980ae284'
  'e1a85727599c145ad026ff8b41c49c55b9bc98e859569535814a70980e6ba709'
  '36ef9b50071cdeb892cb14f3fecf6beb707fca2b50d10ec5a8cdd31b2c0118f7'
  'SKIP'
)

package() {
  # Install binary
  install -Dm 755 "${srcdir}/${pkgname}-bin-${CARCH}-v${pkgver}" "${pkgdir}/usr/bin/${_binname}"

  # Install MIT license
  install -Dm 644 "${srcdir}/LICENSE-MIT-v${pkgver}" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE-MIT"

  # Install shell completions
  install -Dm644 "completions_bash_v${pkgver}" "${pkgdir}/usr/share/bash-completion/completions/${_binname}"
  install -Dm644 "completions_fish_v${pkgver}" "${pkgdir}/usr/share/fish/completions/${_binname}.fish"
  install -Dm644 "completions_zsh_v${pkgver}" "${pkgdir}/usr/share/zsh/site-functions/_${_binname}"
}

# vim:set ts=2 sw=2 et:

