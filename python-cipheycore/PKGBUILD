# Maintainer: Nagy Roland <roliboy@protonmail.com>
# Contributor: Hao Long <aur@esd.cc>

pkgname=python-cipheycore
_pkgname=CipheyCore
pkgver=0.3.2
pkgrel=1
pkgdesc="Some cryptanalysis tidbits written in a proper language"
arch=("any")
url="https://github.com/Ciphey/CipheyCore"
license=('MIT')
depends=('python')
makedepends=('python-setuptools' 'python-poetry' 'boost' 'swig' 'cmake')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha256sums=('d05b4c15077b56121e96c1b934ae2d49f14004834a9b8fbc1bdc76782cd66253')

build() {
  cd ${_pkgname}-${pkgver}
  mkdir build && cd build
  cmake .. -DCIPHEY_CORE_TEST=OFF -DCMAKE_BUILD_TYPE=Release
  cmake --build .
  python -m poetry build
  cd dist
  tar xf cipheycore-${pkgver}.tar.gz
  cd cipheycore-${pkgver}
  python setup.py build
}

package() {
  cd ${_pkgname}-${pkgver}/build/dist/cipheycore-${pkgver}
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 ${srcdir}/${_pkgname}-${pkgver}/LICENSE -t ${pkgdir}/usr/share/licenses/${pkgname}/
}
