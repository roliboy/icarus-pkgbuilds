# Maintainer: Nagy Roland <roliboy@protonmail.com>

pkgname=neovim-airline-themes
pkgver=r1386.63b66df
_gitcommit=63b66df2851c0851df9e8018f62ed7208f3485de
pkgrel=1
pkgdesc='Themes for neovim-airline'
url='https://github.com/vim-airline/vim-airline-themes'
arch=('any')
license=('MIT')
depends=('neovim')
makedepends=('git')
groups=('neovim-plugins')
source=("$pkgname::git+${url}#commit=${_gitcommit}")
sha256sums=('SKIP')

pkgver() {
  cd ${pkgname}
  printf "r%s.%s" \
    "$(git rev-list --count HEAD)" \
    "$(git rev-parse --short HEAD)"
}

package() {
  cd ${pkgname}

  _installpath="${pkgdir}/usr/share/nvim/runtime"
  install -d "${_installpath}"
  cp -r autoload doc plugin "${_installpath}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
